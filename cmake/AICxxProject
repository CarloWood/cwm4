include_guard(GLOBAL)

# This header must be included at the top of the top level CMakeLists.txt right after the `project(...) command`.
if ( NOT CMAKE_PROJECT_NAME )
  message( FATAL_ERROR "Use `include(cwm4/cmake/AICxxProject)` in the top-level CMakeLists.txt BEFORE the `project(...)` command." )
endif ()
message( STATUS "----------------------------------------------------\n** Configuring project ${CMAKE_PROJECT_NAME}:" )

# Default message log level. Use cmake --log-level=debug to get DEBUG output.
set( CMAKE_MESSAGE_LOG_LEVEL "STATUS" )

# Add extra build types.
string( REGEX REPLACE "( -DNDEBUG$|-DNDEBUG )" "" CMAKE_CXX_FLAGS_BETATEST "${CMAKE_CXX_FLAGS_RELEASE}" )
string( REGEX REPLACE "( -DNDEBUG$|-DNDEBUG )" "" CMAKE_C_FLAGS_BETATEST "${CMAKE_C_FLAGS_RELEASE}" )
string( REGEX REPLACE "-DNDEBUG " "" CMAKE_CXX_FLAGS_RELWITHDEBUG "${CMAKE_CXX_FLAGS_RELWITHDEBINFO} -DDEBUG" )
string( REGEX REPLACE "-DNDEBUG " "" CMAKE_C_FLAGS_RELWITHDEBUG "${CMAKE_C_FLAGS_RELWITHDEBINFO} -DDEBUG" )

# Add CMAKE_MODULE_PATH.
list( APPEND CMAKE_MODULE_PATH "${CMAKE_CURRENT_SOURCE_DIR}/cwm4/cmake" )

# Set top_srcdir and top_bindir.
set( top_srcdir "${CMAKE_CURRENT_SOURCE_DIR}" )
set( top_objdir "${CMAKE_CURRENT_BINARY_DIR}" )

# The list of OBJECT libraries that this project uses.
set( AICXX_OBJECTS_LIST "" CACHE INTERNAL "List of OBJECT libaries that this project uses." )

# Does project include Hunter?
if ( NOT COMMAND hunter_add_package )
  # To get rid of this warning, add support for
  # (Hunter)[https://cpp-pm-hunter.readthedocs.io/en/latest/index.html]
  # to your project by adding the following right before the `project(...)`
  # command in the top-level CMakeLists.txt file:
  #
  #     # Note: use -DHUNTER_ENABLED=NO to turn off the usage of Hunter.
  #     include( cwm4/gate/cmake/HunterGate.cmake )
  #     # Hunter Gate to use when Hunter is turned on.
  #     HunterGate(
  #         URL "https://github.com/ruslo/hunter/archive/v0.23.214.tar.gz"
  #         SHA1 "e14bc153a7f16d6a5eeec845fb0283c8fad8c358"
  #     )
  message( WARNING "Support for Hunter is not included. Voiding calls to hunter_add_package." )
  macro(hunter_add_package)
  endmacro ()
endif ()

# We need -pthread because we need _REENTRANT to be defined. See https://stackoverflow.com/a/39547577/1487069
set( THREADS_PREFER_PTHREAD_FLAG true )

# Add support for CMAKE_BUILD_TYPE, EnableDebug, EnableGlobalDebug, EnableLibcwd
include( CW_OPTIONS )

#==============================================================================
# Add target maintainer-clean.
#
add_custom_target( maintainer-clean
    # The current directory is CMAKE_CURRENT_BINARY_DIR.
    COMMAND ${CMAKE_CURRENT_SOURCE_DIR}/cwm4/scripts/cmake_maintainer_clean.sh $(MAKE)
)

# vi:syntax=cmake
